import { uniqueResourceName } from '../../functions/resourceName.bicep'

@description('The prefix used for naming resources to ensure unique names')
param namePrefix string

@description('The location where the resources will be deployed. Defaults to the current resource group location when omitted.')
param location string = resourceGroup().location

@description('The name of the environment Key Vault')
param environmentKeyVaultName string = ''

@description('The name of the secret name(key) in the source key vault to store the PostgreSQL administrator login password')
#disable-next-line secure-secrets-in-params
param srcKeyVaultAdministratorLoginPasswordKey string

@description('The ID of the subnet where the PostgreSQL server will be deployed')
param subnetId string

@description('The ID of the virtual network for the private DNS zone')
param vnetId string

@description('Tags to apply to resources')
param tags object

@description('The name stem to use when deriving the PostgreSQL server resource name. Use lowercase letters, numbers, and hyphens only.')
@minLength(1)
@maxLength(40)
param serverNameStem string

@description('The PostgreSQL major version to provision.')
@allowed([
  '16'
  '17'
  '18'
])
param postgresVersion string

@description('Whether this deployment should publish the canonical Dialogporten connection string secrets.')
param publishCanonicalConnectionSecrets bool = true

@description('Whether to provision Azure Backup Vault for PostgreSQL. (This does not create an Azure Backup vault/RSV for now.)')
param enableBackupVault bool = false

@export()
type Sku = {
  name: 'Standard_B1ms' | 'Standard_B2s' | 'Standard_B4ms' | 'Standard_B8ms' | 'Standard_B12ms' | 'Standard_B16ms' | 'Standard_B20ms' | 'Standard_D4ads_v5' | 'Standard_D8ads_v5' | 'Standard_D16ads_v5' | 'Standard_D32ads_v5' | 'Standard_D48ads_v5' | 'Standard_D64ads_v5' | 'Standard_E2ads_v5' | 'Standard_E4ads_v5' | 'Standard_E8ads_v5' | 'Standard_E16ads_v5' | 'Standard_E20ads_v5' | 'Standard_E32ads_v5' | 'Standard_E48ads_v5' | 'Standard_E64ads_v5' | 'Standard_E96ads_v5'
  tier: 'Burstable' | 'GeneralPurpose' | 'MemoryOptimized'
}

@description('The SKU of the PostgreSQL server')
param sku Sku

@sealed()
type PremiumLrsStorageConfiguration = {
  @minValue(32)
  storageSizeGB: int
  @description('The type of storage account to use.')
  type: 'Premium_LRS'
  @description('Autogrow is used with Premium_LRS.')
  autoGrow: ('Enabled' | 'Disabled')?
  @description('The performance tier of the storage. Use only with Premium_LRS.')
  tier: ('P1' | 'P2' | 'P4' | 'P6' | 'P10' | 'P15' | 'P20' | 'P30' | 'P40' | 'P50' | 'P60' | 'P70' | 'P80')?
}

@sealed()
type PremiumV2LrsStorageConfiguration = {
  @minValue(32)
  storageSizeGB: int
  @description('The type of storage account to use.')
  type: 'PremiumV2_LRS'
  @description('Provisioned IOPS. Required when using PremiumV2_LRS.')
  @minValue(3000)
  iops: int
  @description('Provisioned throughput in MB/s. Required when using PremiumV2_LRS.')
  @minValue(125)
  throughput: int
}

@export()
@discriminator('type')
type StorageConfiguration = PremiumLrsStorageConfiguration | PremiumV2LrsStorageConfiguration

@description('The storage configuration for the PostgreSQL server')
param storage StorageConfiguration

@description('Enable query performance insight')
param enableQueryPerformanceInsight bool

@description('Enable index tuning')
param enableIndexTuning bool

@description('Enable collection of database I/O timing statistics')
param enableTrackIoTiming bool = false

@export()
type ServerConfiguration = {
  @description('The PostgreSQL server parameter name.')
  name: string
  @description('The PostgreSQL server parameter value, without display units.')
  value: string
}

@description('Additional PostgreSQL server parameters to persist as user overrides. Values must not duplicate the module-managed base/query-store settings.')
param additionalServerConfigurations ServerConfiguration[] = []

@description('Static PostgreSQL server parameters to persist but not apply unless applyStaticServerConfigurations is true.')
param staticServerConfigurations ServerConfiguration[] = []

@description('Apply static PostgreSQL server parameters. This may restart the server and should only be enabled in a planned maintenance window.')
param applyStaticServerConfigurations bool = false

@description('The name of the Application Insights workspace')
param appInsightWorkspaceName string

@export()
type HighAvailabilityConfiguration = {
  mode: 'ZoneRedundant' | 'SameZone'
  standbyAvailabilityZone: string
}

@description('High availability configuration for the PostgreSQL server')
param highAvailability HighAvailabilityConfiguration?

@description('The availability zone for the PostgreSQL primary server')
param availabilityZone string

@description('The number of days to retain backups.')
@minValue(7)
@maxValue(35)
param backupRetentionDays int

@description('The Key Vault to store the PostgreSQL administrator login password')
@secure()
param srcKeyVault object

@description('The password for the PostgreSQL administrator login')
@secure()
param administratorLoginPassword string

@description('The name of the deployer principal used as the PostgreSQL administrator')
@minLength(3)
param deployerPrincipalName string

var administratorLogin = 'dialogportenPgAdmin'
var databaseName = 'dialogporten'
var postgresServerNameMaxLength = 63
var postgresServerName = uniqueResourceName('${namePrefix}-${serverNameStem}', postgresServerNameMaxLength, subscription().id, resourceGroup().id)
var shouldPublishCanonicalConnectionSecrets = publishCanonicalConnectionSecrets && !empty(environmentKeyVaultName)
var postgresStorage = storage.type == 'PremiumV2_LRS'
  ? {
      storageSizeGB: storage.storageSizeGB
      type: storage.type
      iops: storage.iops
      throughput: storage.throughput
    }
  : {
      storageSizeGB: storage.storageSizeGB
      autoGrow: storage.autoGrow ?? 'Disabled'
      type: storage.type
      tier: storage.tier
    }

var backupVaultNamePrefix = '${namePrefix}-backupvault'
var restoreContainerName = toLower('${namePrefix}-postgresql-restore')

/*
 These are known (as per 2026-04-18) static PostgreSQL 18 server parameters that require a server restart when updated.

 Can be fetched with:
 az postgres flexible-server parameter list \
  --resource-group <resource_group> \
  --server-name <server> \
  --query "[?isDynamicConfig==\`false\` && isReadOnly==\`false\`] | [].name"
*/
var staticServerConfigurationNames = [
  'autovacuum_freeze_max_age'
  'autovacuum_multixact_freeze_max_age'
  'autovacuum_worker_slots'
  'azure_cdc.max_fabric_mirrors'
  'commit_timestamp_buffers'
  'cron.database_name'
  'cron.log_run'
  'cron.log_statement'
  'cron.max_running_jobs'
  'cron.timezone'
  'duckdb.max_memory'
  'duckdb.max_workers_per_postgres_scan'
  'duckdb.memory_limit'
  'duckdb.threads'
  'duckdb.worker_threads'
  'huge_pages'
  'io_max_concurrency'
  'io_method'
  'max_active_replication_origins'
  'max_connections'
  'max_locks_per_transaction'
  'max_logical_replication_workers'
  'max_prepared_transactions'
  'max_replication_slots'
  'max_wal_senders'
  'max_worker_processes'
  'multixact_member_buffers'
  'multixact_offset_buffers'
  'notify_buffers'
  'pg_stat_statements.max'
  'serializable_buffers'
  'shared_buffers'
  'shared_preload_libraries'
  'subtransaction_buffers'
  'track_activity_query_size'
  'track_commit_timestamp'
  'transaction_buffers'
  'wal_buffers'
  'wal_level'
]
var additionalServerConfigurationStaticChecks = [
  for configuration in additionalServerConfigurations: contains(staticServerConfigurationNames, toLower(configuration.name))
]
var additionalServerConfigurationsContainStatic = contains(additionalServerConfigurationStaticChecks, true)
var additionalServerConfigurationsAreRestartSafe = !additionalServerConfigurationsContainStatic
  ? true
  : fail('Static PostgreSQL server parameters cannot be deployed through additionalServerConfigurations because they may restart the server. Remove static parameters from additionalServerConfigurations.')
var staticServerConfigurationStaticChecks = [
  for configuration in staticServerConfigurations: contains(staticServerConfigurationNames, toLower(configuration.name))
]
var staticServerConfigurationsContainOnlyStatic = !contains(staticServerConfigurationStaticChecks, false)
  ? true
  : fail('Only known static PostgreSQL server parameters can be deployed through staticServerConfigurations.')

// Note: This provisions only the storage primitives used for PostgreSQL restores. The actual Azure Backup vault/RSV is clickopsed for now.
module backupVaultStorageAccount '../storageAccount/main.bicep' = if (enableBackupVault) {
  name: 'backupVaultStorageAccount'
  params: {
    namePrefix: backupVaultNamePrefix
    location: location
    tags: tags
    allowBlobPublicAccess: false
    enableHierarchicalNamespace: false
  }
}

module backupVaultRestoreContainer '../storageContainer/main.bicep' = if (enableBackupVault) {
  name: 'backupVaultRestoreContainer'
  params: {
    storageAccountName: backupVaultStorageAccount.outputs.storageAccountName
    containerName: restoreContainerName
    publicAccess: 'None'
    enableBlobSoftDelete: true
    blobSoftDeleteRetentionDays: 30
  }
}

module saveAdmPassword '../keyvault/upsertSecret.bicep' = {
  name: 'Save_${srcKeyVaultAdministratorLoginPasswordKey}'
  scope: resourceGroup(srcKeyVault.subscriptionId, srcKeyVault.resourceGroupName)
  params: {
    destKeyVaultName: srcKeyVault.name
    secretName: srcKeyVaultAdministratorLoginPasswordKey
    secretValue: administratorLoginPassword
    tags: tags
  }
}

module privateDnsZone '../privateDnsZone/main.bicep' = {
  name: 'postgresqlPrivateDnsZone'
  params: {
    namePrefix: namePrefix
    defaultDomain: '${namePrefix}.postgres.database.azure.com'
    vnetId: vnetId
    tags: tags
  }
}

resource postgresAdminIdentity 'Microsoft.ManagedIdentity/userAssignedIdentities@2024-11-30' = {
  name: '${namePrefix}-postgres-admin-identity'
  location: location
  tags: tags
}

resource postgres 'Microsoft.DBforPostgreSQL/flexibleServers@2025-08-01' = {
  name: postgresServerName
  location: location
  identity: {
    type: 'UserAssigned'
    userAssignedIdentities: {
      '${postgresAdminIdentity.id}': {}
    }
  }
  properties: {
    version: postgresVersion
    administratorLogin: administratorLogin
    administratorLoginPassword: administratorLoginPassword
    storage: postgresStorage
    backup: {
      backupRetentionDays: backupRetentionDays
      geoRedundantBackup: 'Disabled'
    }
    dataEncryption: {
      type: 'SystemManaged'
    }
    authConfig: {
      activeDirectoryAuth: 'Enabled'
      passwordAuth: 'Enabled'
    }
    replicationRole: 'Primary'
    network: {
      delegatedSubnetResourceId: subnetId
      privateDnsZoneArmResourceId: privateDnsZone.outputs.id
    }
    availabilityZone: availabilityZone
    highAvailability: highAvailability
    maintenanceWindow: {
      customWindow: 'Enabled'
      dayOfWeek: 1
      startHour: 3
      startMinute: 0
    }
  }
  sku: sku
  resource database 'databases' = {
    name: databaseName
    properties: {
      charset: 'UTF8'
      collation: 'en_US.utf8'
    }
  }

  tags: tags
}

// Admin records must have a unique objectId per server; the RP does not dedupe by
// objectId when principalName differs, so a stray duplicate will hang this write.
resource postgresAdministrators 'Microsoft.DBforPostgreSQL/flexibleServers/administrators@2025-08-01' = {
  name: deployer().objectId
  parent: postgres
  properties: {
    principalName: deployerPrincipalName
    principalType: 'ServicePrincipal'
    tenantId: deployer().tenantId
  }
}

resource enable_extensions 'Microsoft.DBforPostgreSQL/flexibleServers/configurations@2025-08-01' = {
    parent: postgres
    name: 'azure.extensions'
    properties: {
      value: 'PG_TRGM,BTREE_GIN'
      source: 'user-override'
    }
    dependsOn: [postgresAdministrators]
  }

resource idle_transactions_timeout 'Microsoft.DBforPostgreSQL/flexibleServers/configurations@2025-08-01' = {
  parent: postgres
  name: 'idle_in_transaction_session_timeout'
  properties: {
    value: '86400000' // 24 hours
    source: 'user-override'
  }
  dependsOn: [enable_extensions]
}

// Enable Query Store when either index tuning or query performance insight is enabled
var enableQueryStore = enableIndexTuning || enableQueryPerformanceInsight

resource track_io_timing 'Microsoft.DBforPostgreSQL/flexibleServers/configurations@2025-08-01' = if (enableTrackIoTiming || enableQueryStore) {
  parent: postgres
  name: 'track_io_timing'
  properties: {
    value: 'on'
    source: 'user-override'
  }
  dependsOn: [idle_transactions_timeout]
}

resource pg_qs_query_capture_mode 'Microsoft.DBforPostgreSQL/flexibleServers/configurations@2025-08-01' = if (enableQueryStore) {
  parent: postgres
  name: 'pg_qs.query_capture_mode'
  properties: {
    value: 'all'
    source: 'user-override'
  }
  dependsOn: [track_io_timing]
}

resource pgms_wait_sampling_query_capture_mode 'Microsoft.DBforPostgreSQL/flexibleServers/configurations@2025-08-01' = if (enableQueryPerformanceInsight) {
  parent: postgres
  name: 'pgms_wait_sampling.query_capture_mode'
  properties: {
    value: 'all'
    source: 'user-override'
  }
  dependsOn: [pg_qs_query_capture_mode, track_io_timing, idle_transactions_timeout, enable_extensions]
}

resource index_tuning_mode 'Microsoft.DBforPostgreSQL/flexibleServers/configurations@2025-08-01' = if (enableIndexTuning) {
  parent: postgres
  name: 'index_tuning.mode'
  properties: {
    value: 'report'
    source: 'user-override'
  }
  dependsOn: [pgms_wait_sampling_query_capture_mode, pg_qs_query_capture_mode, track_io_timing, idle_transactions_timeout, enable_extensions]
}

@batchSize(1)
resource additionalPostgresServerConfigurations 'Microsoft.DBforPostgreSQL/flexibleServers/configurations@2025-08-01' = [for configuration in additionalServerConfigurations: if (additionalServerConfigurationsAreRestartSafe) {
  parent: postgres
  name: configuration.name
  properties: {
    value: configuration.value
    source: 'user-override'
  }
  dependsOn: [index_tuning_mode, pgms_wait_sampling_query_capture_mode, pg_qs_query_capture_mode, track_io_timing, idle_transactions_timeout, enable_extensions]
}]

@batchSize(1)
resource staticPostgresServerConfigurations 'Microsoft.DBforPostgreSQL/flexibleServers/configurations@2025-08-01' = [for configuration in staticServerConfigurations: if (applyStaticServerConfigurations && staticServerConfigurationsContainOnlyStatic) {
  parent: postgres
  name: configuration.name
  properties: {
    value: configuration.value
    source: 'user-override'
  }
  dependsOn: [additionalPostgresServerConfigurations]
}]

resource appInsightsWorkspace 'Microsoft.OperationalInsights/workspaces@2025-07-01' existing = {
  name: appInsightWorkspaceName
}

// todo: setting as 0 for now. Will use the log analytics workspace policy instead. Consider setting explicitly in the future.
var diagnosticSettingRetentionPolicy = {
  days: 0
  enabled: false
}

var diagnosticLogCategories = [
  'PostgreSQLLogs'
  'PostgreSQLFlexSessions'
  'PostgreSQLFlexQueryStoreRuntime'
  'PostgreSQLFlexQueryStoreWaitStats'
  'PostgreSQLFlexTableStats'
  'PostgreSQLFlexDatabaseXacts'
]

resource diagnosticSetting 'Microsoft.Insights/diagnosticSettings@2021-05-01-preview' = {
  name: 'PostgreSQLDiagnosticSetting'
  scope: postgres
  properties: {
    workspaceId: appInsightsWorkspace.id
    logAnalyticsDestinationType: 'Dedicated'
    logs: [for category in diagnosticLogCategories: {
      category: category
      enabled: true
      retentionPolicy: diagnosticSettingRetentionPolicy
    }]
    metrics: [
      {
        timeGrain: null
        enabled: true
        retentionPolicy: diagnosticSettingRetentionPolicy
        category: 'AllMetrics'
      }
    ]
  }
}

module adoConnectionString '../keyvault/upsertSecret.bicep' = if (shouldPublishCanonicalConnectionSecrets) {
  name: 'adoConnectionString'
  params: {
    destKeyVaultName: environmentKeyVaultName
    secretName: 'dialogportenAdoConnectionString'
    secretValue: 'Server=${postgres.properties.fullyQualifiedDomainName};Database=${databaseName};Port=5432;User Id=${administratorLogin};Password=${administratorLoginPassword};Ssl Mode=Require;Trust Server Certificate=true;Include Error Detail=True;'
    tags: tags
  }
}

module psqlConnectionString '../keyvault/upsertSecret.bicep' = if (shouldPublishCanonicalConnectionSecrets) {
  name: 'psqlConnectionString'
  params: {
    destKeyVaultName: environmentKeyVaultName
    secretName: 'dialogportenPsqlConnectionString'
    secretValue: 'psql \'host=${postgres.properties.fullyQualifiedDomainName} port=5432 dbname=${databaseName} user=${administratorLogin} password=${administratorLoginPassword} sslmode=require\''
    tags: tags
  }
}

output serverName string = postgres.name
output fullyQualifiedDomainName string = postgres.properties.fullyQualifiedDomainName
output adoConnectionStringSecretUri string = shouldPublishCanonicalConnectionSecrets ? adoConnectionString.outputs.secretUri : ''
output psqlConnectionStringSecretUri string = shouldPublishCanonicalConnectionSecrets ? psqlConnectionString.outputs.secretUri : ''
